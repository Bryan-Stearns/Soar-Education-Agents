sp {propose*preflearn*input-option
    "Propose any of the options given on the input-link"
    (state <s> ^io.input-link <il>)
    (<il> ^option <opt>)
    -->
    (<s> ^operator <o> +)
    (<o> ^name choose-option
        ^option <opt>)}

sp {apply*preflearn*input-option
    "Output the selected option"
    (state <s> ^operator <o>
            ^io.output-link <ol>)
    (<o> ^name choose-option
        ^option <opt>)
    -->
    (<ol> ^proposed-option <opt>)}

sp {apply*preflearn*input-option*remove-output
    (state <s> ^operator <o>
            ^io.output-link <ol>)
    (<o> ^name choose-option
        ^option <opt>)
    (<ol> ^proposed-option {<> <opt> <opt2>})
-->
    (<ol> ^proposed-option <opt2> -)}

sp {propose*preflearn*confirm-option
    (state <s> ^io.input-link.correct-choice <cc>)
    (<cc> ^preferred-option <po>
        -^rejected-option <ro>)
-->
    (<s> ^operator <o> + >)
    (<o> ^name select-option
        ^option <po>)}

sp {apply*preflearn*confirm-option
    (state <s> ^operator <o>
            ^io.output-link <ol>)
    (<o> ^name select-option
        ^option <opt>)
-->
    (<ol> ^selected-option <opt>)}


sp {propose*preflearn*reject-option
    (state <s> ^io.input-link <il>
            ^operator <o1> +)
    (<o1> ^option <ro>
        ^name choose-option)
    (<il> ^correct-choice <cc>)
    (<cc> ^rejected-option <ro>
        ^reason-attribute <ra>
        ^reason-type <rt>
        -^preferred-option <po>)
-->
    (<s> ^operator <o> + >)
    (<o> ^name reject-option
        ^option <ro>)}

sp {apply*preflearn*reject-option
    (state <s> ^operator <o1>
            ^operator <o2> +
            ^io.output-link <ol>)
    (<o1> ^name reject-option
        ^option <opt>)
    (<o2> ^name choose-option
        ^option <opt>)
-->
    (<s> ^operator <o2> -)}


sp {propose*prefleran*store-correct-choice
    "When the agent sees feedback, save the pattern to smem"
    (state <s> ^io.input-link.correct-choice <cc>
                ^smem.command <scmd>)
    (<cc> ^preferred-option <po>
        ^rejected-option <ro>
        -^status success)
-->
    (<s> ^operator <o> + >)
    (<o> ^name store-correct-choice)}


sp {apply*preflearn*store-correct-chooice
    (state <s> ^operator <o>
            ^io.input-link.correct-choice <cc>
            ^smem.command <scmd>)
    (<cc> ^preferred-option <po>
        ^rejected-option <ro>)
    (<o> ^name store-correct-choice)
-->
    (<scmd> ^store <cc> <po> <ro>)
    (<cc> ^status success)}


sp {set*smem*clean*store
    "Clean any store command from smem when its complete"
    :o-support
    (state <s> ^smem <smem>)
    (<smem> ^command <scmd>
            ^result.success <store>)
    (<scmd> ^store <store>)
-->
    (<scmd> ^store <store> -)}


# TIE IMPASSE SUBSTATE

sp {propose*preflearn-tie*get-help
    "Pick two of the tied options to ask for help about"
    (state <s> ^impasse tie
            ^item <i1> 
            ^item {<i2> > <i1>}  # Make sure the second item has a higher ID than the first, so that we avoid duplicate rule matches
            ^correct-choice-smem none) 
    (<i1> ^option <p1>)
    (<i2> ^option <p2>)
    -->
    (<s> ^operator <o> + =)
    (<o> ^name get-help
        ^option <p1> <p2>)}

sp {apply*preflearn-tie*get-help
    "Send a request for help on the topstate output-link"
    (state <s> ^operator <o>
            ^quiescence t           # This tests quiescence, effectively testing that the impasse itself is needed for our result of asking for help
            ^superstate.io.output-link <sol>)
    (<o> ^name get-help
        ^option <p1> 
        ^option {<p2> > <p1>})      # Avoid duplicate rule matches the same way as in the proposal rule
    -->
    (<sol> ^help-request <h-new>)
    (<h-new> ^option <p1> 
            ^option <p2>)}

sp {propose*preflearn-tie*create-preference
    (state <s> ^superstate <ss> 
            ^item <o1> 
            ^item <o2>)
    (<o1> ^option {<item-opt1>}
        ^name choose-option)
    (<opt1> ^name <opt1-name>)
    (<item-opt1> ^name <opt1-name>)
    (<o2> ^option {<item-opt2> @ <opt2>}
        ^name choose-option)
    (<s> ^correct-choice-smem <cc>)
    (<cc> ^preferred-option <opt1> 
        ^rejected-option <opt2> 
        ^reason-attribute <attr> )
    (<item-opt2> ^<attr>)
-->
    (<s> ^operator <o> +)
    (<o> ^name create-preference
        ^preferred-item <o1>
        ^rejected-item <o2>
        ^reason-attribute <attr> )
}
sp {apply*preflearn-tie*create-preference-positive
    "From the tie substate, return a preference to the topstate based on input help"
    (state <s> ^correct-choice-smem <cc>
        ^operator <o>
        ^superstate <ss>)
    (<cc> ^reason-type positive)
    (<o> ^name create-preference
        ^preferred-item <o1>
        ^rejected-item <o2>
        ^reason-attribute <attr> )
    (<o1> ^option <item-opt1>)
    (<o2> ^option <item-opt2>)
    (<item-opt1> ^<attr> <attr-value>)
    (<item-opt2> ^<attr> < <attr-value>)
    #(<item-opt1> ^name <opt1-name>)
    #(<item-opt2> ^name <opt2-name>)
-->
    (<ss> ^operator <o1> > <o2>)
    (<s> ^correct-choice-smem <cc> -)}

sp {apply*preflearn-tie*create-preference-negative
    "From the tie substate, return a preference to the topstate based on input help"
    (state <s> ^correct-choice-smem <cc>
        ^operator <o>
        ^superstate <ss>)
    (<cc> ^reason-type negative)
    (<o> ^name create-preference
        ^rejected-item <o2>
        ^reason-attribute <attr> )
    (<o2> ^option <item-opt2>)
    (<item-opt2> ^<attr> <attr-value>)
    #(<item-opt1> ^name <opt1-name>)
    #(<item-opt2> ^name <opt2-name>)
-->
    (<ss> ^operator <o2> <)
    (<s> ^correct-choice-smem <cc> -)
}


sp {propose*query-correct-choice
    (state <s> ^impasse tie) #need to test for substate
    (<s> -^correct-choice-smem <cc>)
-->
    (<s> ^operator <o> + = <)
    (<o> ^name query-correct-choice)
}

sp {apply*query-correct-choice
    (state <s> ^operator.name query-correct-choice)
    (<s> ^smem.command <scmd>)
-->
    (<scmd> ^query <q> ^depth 2)
    (<q> ^preferred-option <po>
         ^rejected-option <ro>)
}

sp {propose*preflearn*query-cc-success
    (state <s> ^smem.result.success <q>)
    (<q> ^preferred-option <po>
        ^rejected-option <ro>)
-->
    (<s> ^operator <o> +)
    (<o> ^name query-cc-success)
}

sp {apply*preflearn*query-cc-success*create-cc-smem
    (state <s> ^operator.name query-cc-success
            ^smem <smem>)
    (<smem> ^result.retrieved <ret>
            ^command <cmd>)
    (<cmd> ^query <q>
        ^depth <d>)
    (<ret> ^preferred-option <po>
        ^rejected-option <ro>)
-->
    (<s> ^correct-choice-smem (deep-copy <ret>))
}

sp {apply*preflearn*query-cc-success*link-cc-ret
    (state <s> ^operator.name query-cc-success
            ^smem <smem>
            ^correct-choice-smem <cc>)
    (<cc> ^preferred-option <po-cc>
        ^rejected-option <ro-cc>)
    (<smem> ^result.retrieved <ret>
            ^command <cmd>)
    (<cmd> ^query <q>
        ^depth <d>)
    (<ret> ^preferred-option <po>
        ^rejected-option <ro>)
-->
    (link-stm-to-ltm <cc> (@ <ret>))
    (link-stm-to-ltm <po-cc> (@ <po>))
    (link-stm-to-ltm <ro-cc> (@ <ro>))
    (<cmd> ^query <q> -
        ^depth <d> -
        ^store <ret>)
    (<ret> ^preferred-option <po> -
        ^rejected-option <ro> -)
}

sp {propose*preflearn*query-cc-failure
    (state <s> ^smem.result.failure <q>)
    (<q> ^preferred-option <po>
        ^rejected-option <ro>)
-->
    (<s> ^operator <o> +)
    (<o> ^name query-cc-failure)
}


sp {apply*preflearn*query-cc-failure
    (state <s> ^operator.name query-cc-failure
            ^smem <smem>)
    (<smem> ^command <cmd>)
    (<cmd> ^query <q>
        ^depth <d>)
-->
    (<s> ^correct-choice-smem none)
    (<cmd> ^query <q> -
        ^depth <d> -)
}
